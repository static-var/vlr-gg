// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext {
        compose_compiler = '1.3.0'
        hilt_version = '2.43.2'
        kotlin_version = '1.7.10'
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.0-alpha09'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
        classpath "com.google.dagger:hilt-android-gradle-plugin:$hilt_version"
        classpath 'com.google.gms:google-services:4.3.13'
        classpath "org.jetbrains.kotlinx:kover:0.5.1"
        classpath 'com.google.firebase:perf-plugin:1.4.1'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.9.1'
    }
}

plugins {
    id "com.diffplug.spotless" version "6.9.1"
    id 'org.jetbrains.kotlinx.kover' version '0.6.0'
}

spotless {
    kotlin {
        ktfmt().googleStyle()
        target("**/*.kt")
        targetExclude("**/build/")
    }
    format("xml") {
        target("**/*.xml")
        targetExclude("**/build/", ".idea/")
        trimTrailingWhitespace()
        indentWithSpaces()
        endWithNewline()
    }
}
//
//task clean(type: Delete) {
//    delete rootProject.buildDir
//}

subprojects {
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            if (project.findProperty("enableComposeCompilerReports") == "true") {
                freeCompilerArgs += [
                        "-P",
                        "plugin:androidx.compose.compiler.plugins.kotlin:reportsDestination=" +
                                project.buildDir.absolutePath + "/compose_metrics"
                ]
                freeCompilerArgs += [
                        "-P",
                        "plugin:androidx.compose.compiler.plugins.kotlin:metricsDestination=" +
                                project.buildDir.absolutePath + "/compose_metrics"
                ]
            }
        }
    }
}

tasks.koverMergedHtmlReport {
    enabled = true                          // false to disable report generation
    htmlReportDir.set(layout.buildDirectory.dir("my-merged-report/html-result"))
    includes = ['dev.staticvar.vlr.*']             // inclusion rules for classes
}